# NAME

MooX::TaggedAttributes - Add a tag with an arbitrary value to a an attribute

# VERSION

version 0.08

# SYNOPSIS

    # define a Tag Role
    package T1;
    use Moo::Role;
    
    use MooX::TaggedAttributes -tags => [qw( t1 t2 )];
    1;

    # Apply a tag role directly to a class
    package C1;
    use Moo;
    use T1;
    
    has c1 => ( is => 'ro', t1 => 1 );
    1;

    # use a tag role in another Role
    package R1;
    
    use Moo::Role;
    use T1;
    
    has r1 => ( is => 'ro', t2 => 2 );
    1;

    # Use a tag role which consumes a tag role in a class
    package C2;
    use Moo;
    use R1;
    
    has c2 => ( is => 'ro', t2 => sub { } );
    1;

    # Use our tags
    use C1;
    use C2;
    
    use 5.010;
    
    # get the value of the tag t1, applied to attribute a1
    say C1->new->_tags->{t1}{a1};
    
    # get the value of the tag t2, applied to attribute c2
    say C2->new->_tags->{t2}{c2};

# DESCRIPTION

This module attaches a tag-value pair to an attribute in a **Moo**
class or role, and provides a interface to query which attributes have
which tags, and what the values are.

## Tagging Attributes

To define a set of tags, create a special _tag role_:

    package T1;
    use Moo::Role;
    use MooX::TaggedAttributes -tags => [ 't1' ];
    
    has a1 => ( is => 'ro', t1 => 'foo' );
    
    1;

If there's only one tag, it can be passed directly without being
wrapped in an array:

    package T2;
    use Moo::Role;
    use MooX::TaggedAttributes -tags => 't2';
    
    has a2 => ( is => 'ro', t2 => 'bar' );
    
    1;

A tag role is a standard **Moo::Role** with added machinery to track
attribute tags.  As shown, attributes may be tagged in the tag role
as well as in modules which consume it.

Tag roles may be consumed just as ordinary roles, but in order for
role consumers to have the ability to assign tags to attributes, they
need to be consumed with the Perl **use** statement, not with the **with** statement.

Consuming with the **with** statement _will_ propagate attributes with
existing tags, but won't provide the ability to tag new attributes.

This is correct:

    package R2;
    use Moo::Role;
    use T1;
    
    has r2 => ( is => 'ro', t1 => 'foo' );
    1;

    package R3;
    use Moo::Role;
    use R3;
    
    has r3 => ( is => 'ro', t1 => 'foo' );
    1;

The same goes for classes:

    package C1;
    use Moo;
    use T1;
    
    has c1 => ( is => 'ro', t1 => 'foo' );
    1;

Combining tag roles is as simple as **use**'ing them in the new role:

    package T12;
    
    use Moo::Role;
    use T1;
    use T2;
    
    1;

    package C2;
    use Moo;
    use T12;
    
    has c2 => ( is => 'ro', t1 => 'foo', t2 => 'bar' );
    1;

## Accessing tags

Classes and objects are provided a **\_tags** method which returns a
hash of hashes keyed off of the tags and attribute names.  For
example, for the following code:

    package T;
    use Moo::Role;
    use MooX::TaggedAttributes -tags => [qw( t1 t2 )];
    1;

    package C;
    use Moo;
    use T;
    
    has a => ( is => 'ro', t1 => 2 );
    has b => ( is => 'ro', t2 => 'foo' );
    1;

The tag structure returned by  `C->_tags`

    { t1 => { a => 2 }, t2 => { b => "foo" } }

and `C->new->_tags`

    { t1 => { a => 2 }, t2 => { b => "foo" } }

are identical.

# BUGS AND LIMITATIONS

## Changes to an object after instantiation are not tracked.

If a role with tagged attributes is applied to an object, the
tags for those attributes are not visible.

# BUGS

Please report any bugs or feature requests on the bugtracker website
[https://rt.cpan.org/Public/Dist/Display.html?Name=MooX-TaggedAttributes](https://rt.cpan.org/Public/Dist/Display.html?Name=MooX-TaggedAttributes)
or by email to
[bug-MooX-TaggedAttributes@rt.cpan.org](mailto:bug-MooX-TaggedAttributes@rt.cpan.org).

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

# SOURCE

The development version is on github at [https://github.com/djerius/moox-taggedattributes](https://github.com/djerius/moox-taggedattributes)
and may be cloned from [git://github.com/djerius/moox-taggedattributes.git](git://github.com/djerius/moox-taggedattributes.git)

# AUTHOR

Diab Jerius <djerius@cpan.org>

# COPYRIGHT AND LICENSE

This software is Copyright (c) 2018 by Smithsonian Astrophysical Observatory.

This is free software, licensed under:

    The GNU General Public License, Version 3, June 2007
